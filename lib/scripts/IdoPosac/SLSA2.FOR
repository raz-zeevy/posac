
CY$STORAGE:2
      PROGRAM LSA2
C***************************************************************
C
C  LSA2 : MAPPING ITEMS BY THE GENERIC ROLES THEY PLAY IN
C         THE SCALOGRAM
C
C***************************************************************
C
C  THIS PROGRAM IS RUN AFTER PROGRAM 'POSAC' WITHOUT ANY
C  ADDITIONAL DIRECTIVES . IT WORKS ON RESULTS WRITTEN
C  BY 'POSAC' ON UNIT 'NSCR'
C
C***************************************************************
C      COMMON X(5000)
      IMPLICIT INTEGER*2  (I-N)
INCLUDE 'MPARMLIN'
      COMMON X1 (MNV, MIEND), XN1 (MIEND), XN2 (MIEND), XN3 (MIEND),
     $       XN4 (MIEND), XN5 (MNV, 2), XN6 (MNV, 2)
      INTEGER*2 X1, XN1, XN4, XN5
      REAL XN2, XN3, XN6
      COMMON/IO/NWRITE,NSCR
      COMMON/LAB/LABEL (26)
      CHARACTER*40 LABEL
      COMMON/EP/EPS
      COMMON/SCHARS/UP,BOTTM,LEFT,RIGHT,
     $               UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT,FORMFEED
      CHARACTER*1 UP,BOTTM,LEFT,RIGHT,
     $             UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT,FORMFEED
      EPS=1.E-5
      CALL OPENFILE
      READ (NSCR) UP,BOTTM,LEFT,RIGHT,UPLEFT,UPRIGHT,
     $            BOTTMLEFT,BOTTMRIGHT,FORMFEED
      READ(NSCR) NV,NP
      READ(NSCR) (LABEL (I), I = 1, NV)
      WRITE(NWRITE,1000) FORMFEED
C      N1=NV*NP+1
C      N2=N1+NP
C      N3=N2+NP
C      N4=N3+NP
C      N5=N4+NP
C      N6=N5+NV*2
C      N7=N6+NV*2
C      MEMORY=NP*(4+NV)+4*NV
      CALL LSREAD (X1, XN1, XN2, XN3, NV, NP)
      CALL SUBLSA (X1, XN1, XN2, XN3, XN4, XN5, XN6, NV, NP)
      STOP 'LSA2 Completed'
 1000 FORMAT(A1,/,40X,55(1H*),/,40X,1H*,15X,
     $22HLATTICE SPACE ANALYSIS,16X,1H*,/,40X,1H*,
     $36HLSA2 BASED ON SIMILARITY OF ITEMS TO,
     $17H FOUR IDEAL TYPES,1H*,/,40X,55(1H*),///)
      END

      SUBROUTINE LSREAD(IPRF,IFREQ,XX,YY,NV,NP)
      IMPLICIT INTEGER*2  (I-N)
      COMMON/IO/NWRITE,NSCR
      DIMENSION IPRF(NV,1),IFREQ(1),XX(1),YY(1)
      DO 10 I=1,NP
         READ(NSCR) (IPRF(K,I),K=1,NV),ISCO,IFREQ(I),XX(I),YY(I)
   10 CONTINUE
      RETURN
      END

      SUBROUTINE DEGM12(IPRF,XY,IVY,ITEM,NV,NP)
      IMPLICIT INTEGER*2  (I-N)
      COMMON/EP/EPS
      DIMENSION IPRF(NV,1),XY(1),IVY(1)
C  NCAT=NUMBER OF CATEGORIES IN ITEM
      MXCAT=IPRF(ITEM,1)
      MNCAT=IPRF(ITEM,NP)
      NCAT=MXCAT-MNCAT+1
      DELTAX=100./NCAT
      DELTAX=DELTAX+EPS
      DO 10 I=1,NP
         DO 20 IX=1,NCAT
            XLOW=(IX-1)*DELTAX
            XUPP=IX*DELTAX
            IF(XY(I).LT.XLOW) GO TO 20
            IF(XY(I).GT.XUPP) GO TO 20
            IVY(I)=MNCAT+IX-1
   20    CONTINUE
   10 CONTINUE
      RETURN
      END

      SUBROUTINE MU2(IPRF,IFREQ,IVY,ITEM,NV,NP,COEFF)
      IMPLICIT INTEGER*2  (I-N)
      DIMENSION IPRF(NV,1),IFREQ(1),IVY(1)
      U=0.
      V=0.
      COEFF=0.
      N1=NP-1
      DO 10 K=1,N1
         IFK=IFREQ(K)
         IXX=IPRF(ITEM,K)
         IYY=IVY(K)
         L1=K+1
         DO 20 L=L1,NP
            CNUM=IFK*IFREQ(L)*(IXX-IPRF(ITEM,L))*(IYY-IVY(L))
            CDEN=ABS(CNUM)
            U=U+CNUM
            V=V+CDEN
   20    CONTINUE
   10 CONTINUE
      IF(V.LT.1.E-20) RETURN
      COEFF=U/V
      RETURN
      END

      SUBROUTINE SUBLSA(IPRF,IFREQ,XX,YY,IVY,XY,SOL,NV,NP)
      IMPLICIT INTEGER*2  (I-N)
      COMMON/IO/NWRITE,NSCR
      DIMENSION IPRF(NV,1),IFREQ(1),XX(1),YY(1),IVY(1),SOL(NV,2)
      INTEGER*2 XY(NV,2)
      DO 10 K=1,NV
         CALL DEGM12(IPRF,XX,IVY,K,NV,NP)
         CALL MU2(IPRF,IFREQ,IVY,K,NV,NP,C1)
         CALL DEGM12(IPRF,YY,IVY,K,NV,NP)
         CALL MU2(IPRF,IFREQ,IVY,K,NV,NP,C2)
         CALL DEGM3(IPRF,XX,YY,IVY,K,NV,NP)
         CALL MU2(IPRF,IFREQ,IVY,K,NV,NP,C3)
         CALL DEGM4(IPRF,XX,YY,IVY,K,NV,NP)
         CALL MU2(IPRF,IFREQ,IVY,K,NV,NP,C4)
         WRITE(NWRITE,100) K,C1,C2,C3,C4
         SOL(K,1)=C1-C2
         SOL(K,2)=C3-C4
   10 CONTINUE
      WRITE(NWRITE,200)
      CALL TRANS(SOL,NV)
      CALL SPLSA2(SOL,XY,NV)
      CALL PLSA2(SOL,XY,NV)
      RETURN
  100 FORMAT(5X,5HITEM ,I2,8X,3HC1=,F7.4,5X,
     $3HC2=,F7.4,5X,3HC3=,F7.4,5X,3HC4=,F7.4,/)
  200 FORMAT(//,8X,17HMEANING OF THE CI,/,8X,17(1H-),/,
     $8X,42HC1    FIT WITH VERTICAL          PARTITION,/,
     $8X,42HC2    FIT WITH HORIZONTAL        PARTITION,/,
     $8X,42HC3    FIT WITH L-SHAPED          PARTITION,/,
     $8X,42HC4    FIT WITH INVERTED L-SHAPED PARTITION,/)
      END

      SUBROUTINE DEGM3(IPRF,XX,YY,IVY,ITEM,NV,NP)
      IMPLICIT INTEGER*2  (I-N)
      DIMENSION IPRF(NV,1),XX(1),YY(1),IVY(1)
      COMMON/EP/EPS
      MXCAT=IPRF(ITEM,1)
      MNCAT=IPRF(ITEM,NP)
      NCAT=MXCAT-MNCAT+1
      DXY=100./NCAT
      DXY=DXY+EPS
      DO 10 I=1,NP
         XY1=0.
         DO 30 IXY=1,NCAT
            XY2=XY1+DXY
            IF(IXY.EQ.1) XY2=XY2-DXY/2.
            IF(IXY.EQ.NCAT) XY2=XY2+DXY/2.
            IF(XX(I).LT.XY1) GO TO 20
            IF(YY(I).LT.XY1) GO TO 20
            IF(XX(I).GT.XY2.AND.YY(I).GT.XY2) GO TO 20
            IVY(I)=MNCAT+IXY-1
   20       CONTINUE
            XY1=XY2
   30    CONTINUE
   10 CONTINUE
      RETURN
      END

      SUBROUTINE DEGM4(IPRF,XX,YY,IVY,ITEM,NV,NP)
      IMPLICIT INTEGER*2  (I-N)
      DIMENSION IPRF(NV,1),XX(1),YY(1),IVY(1)
      COMMON/EP/EPS
      MXCAT=IPRF(ITEM,1)
      MNCAT=IPRF(ITEM,NP)
      NCAT=MXCAT-MNCAT+1
      DXY=100./NCAT
      DXY=DXY+EPS
      DO 10 I=1,NP
         XY1=0.
         DO 30 IXY=1,NCAT
            XY2=XY1+DXY
            IF(IXY.EQ.1) XY2=XY2+DXY/2.
            IF(IXY.EQ.NCAT) XY2=XY2-DXY/2.
            IF(XX(I).LT.XY1.AND.YY(I).LT.XY1) GO TO 20
            IF(XX(I).GT.XY2) GO TO 20
            IF(YY(I).GT.XY2) GO TO 20
            IVY(I)=MNCAT+IXY-1
   20       CONTINUE
            XY1=XY2
   30    CONTINUE
   10 CONTINUE
      RETURN
      END

      SUBROUTINE TRANS(SOL,NV)
      IMPLICIT INTEGER*2  (I-N)
      COMMON/LAB/LABEL (26)
      CHARACTER*40 LABEL
      COMMON/IO/NWRITE,NSCR
      DIMENSION SOL(NV,2)
      DO 10 I=1,2
         DO 20 K=1,NV
            SOL(K,I)=(SOL(K,I)+2.)*25.
   20    CONTINUE
   10 CONTINUE
      WRITE(NWRITE,100)
      DO 30 K=1,NV
         WRITE(NWRITE,200) K,SOL(K,1),SOL(K,2), LABEL(K)
   30 CONTINUE
      RETURN
  100 FORMAT(//,15X,16HPLOT COORDINATES,//2X,11HITEM NUMBER,
     $9X,1HX,11X,1HY,/,2X,11(1H-),9X,1H-,11X,1H-)
  200 FORMAT(/9X,I2,6X,F6.2,6X,F6.2,4X,A)
      END
C
      SUBROUTINE PLSA2(SOL,IXY,N)
C  THIS SUB. PLOTS FOR SOLUTION SOL
C  LSA2 DIAGRAM
C  IXY IS A WORKING AREA
C  THE INPUT COORDINATES SOL(.,1) (FIRST AXIS)
C  AND SOL(.,J) (SECOND AXIS) HAVE TO LIE
C  BETWEEN 0. AND 100.
C
      IMPLICIT INTEGER*2  (I-N)

      INTEGER*2 IXY(N,2)
      INTEGER*2 IW(51), IBLANK, IPLUS
C
      CHARACTER*4 FOR(56)
      COMMON/SCHARS/UP,BOTTM,LEFT,RIGHT,
     $               UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT,FORMFEED
      CHARACTER*1 UP,BOTTM,LEFT,RIGHT,
     $             UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT,FORMFEED
      CHARACTER*4 F1,F56,F3,F55,SK1,BLANK,FALF,FINT
      CHARACTER*5 I100,I50,I0,IBL
C
CY      DIMENSION SOL(N,1)
      DIMENSION SOL(N,2)
      COMMON/IO/NWRITE,NSCR
      DATA F1,F56,F3,F55/4H(3X,,4H,A) ,4HA1  ,4H,A1 /
      DATA SK1/ 4HA5,  /
      DATA BLANK,FALF,FINT/4H    , 4H,A2 ,4H,I2 /
      DATA IBLANK, IPLUS / 2H    ,2H +   /
      DATA I100,I50,I0,IBL/5H100.. ,5H 50..  ,5H  0.. ,5H        /
C     DATA UP,BOTTM,LEFT,RIGHT/'Í','Í','º','º'/
C     DATA UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT / 'É', '»', 'È', '¼' /
C
C  THIS PLOT IS AVAILABLE FOR 51
C  POSITIONS IN EACH AXIS SINCE THE
C  COORDINATES GO FROM 0./2 TO 100./2
C
C     FORMFEED = CHAR (12)
      NCOOR=50
      NPOS=NCOOR+1
      NFOR=3+NPOS+2
C
C  CLEAR FORMAT ARRAY
      DO 1 K=1,NFOR
         FOR(K)=BLANK
    1 CONTINUE
      FOR(1)=F1
      FOR(2)=SK1
      FOR(3)=F3
      FOR(NFOR-1)=F55
      FOR(NFOR)=F56
C
C  POINT COORDINATES ON FIRST AXIS
      DO 3 K=1,N
         IXY(K,1)=.5*SOL(K,1)+1.5
    3 CONTINUE
C
C  POINT COORDINATES ON SECOND AXIS
      DO 71 K=1,N
         LL=.5*SOL(K,2)+1.5
         IXY(K,2)=NPOS+1-LL
   71 CONTINUE
      WRITE(NWRITE,1000) FORMFEED
      WRITE(NWRITE,1010)
      WRITE(NWRITE,72)
   72 FORMAT(/)
      WRITE(NWRITE,1001) UPLEFT, (UP,JJ=1,102), UPRIGHT
      DO 18 KY=1,NPOS
         DO 7 K=1,NPOS
            IW(K)=IBLANK
            K3=K+3
            FOR(K3)=FALF
    7    CONTINUE
         DO 9 KJ=1,N
            IF(IXY(KJ,2).NE.KY) GO TO 9
            K=IXY(KJ,1)
            K3=K+3
            FOR(K3)=FINT
            IW(K)=KJ
    9    CONTINUE
C
       IF(KY.EQ.1) THEN
           WRITE(NWRITE,FOR) I100, LEFT, IW, RIGHT
       ELSE IF(KY.EQ.2) THEN
           WRITE(NWRITE,FOR) IBL, LEFT, IW, RIGHT, ' L-SHAPED'
       ELSE IF(KY.EQ.(NPOS+1)/2) THEN
C 1033 FORMAT(8H+   50..,28X,1H+,23X,1H+,25X,1H+)
           IF (IW(14) .EQ. IBLANK) IW(14) = IPLUS
           IF (IW(26) .EQ. IBLANK) IW(26) = IPLUS
           IF (IW(39) .EQ. IBLANK) IW(39) = IPLUS
           WRITE(NWRITE,FOR) I50, LEFT, IW, RIGHT
       ELSE IF(KY.EQ.NPOS) THEN
           WRITE(NWRITE,FOR) I0, LEFT, IW, RIGHT, ' INVERTED L-SHAPED'
       ELSE
           WRITE(NWRITE,FOR) IBL, LEFT, IW, RIGHT
       END IF
C
   18 CONTINUE
      WRITE(NWRITE,1001) BOTTMLEFT, (BOTTM,JJ=1,102), BOTTMRIGHT
      WRITE(NWRITE,1222)
      RETURN
C
C  THE FOLLOWING FORMATS ARE
C  AVAILABLE FOR 51 POSITIONS
 1001 FORMAT(8X,104A1)
 1222 FORMAT(2(10X,1H.,49X,1H.,49X,1H.,/),
     $10X,1H0,48X,2H50,48X,3H100//)
C
 1000 FORMAT(A1,52X,12HLSA2 DIAGRAM,/,33X,
     $33HITEM MAPPING BASED ON SIMILARITY ,
     $19HTO FOUR IDEAL TYPES)
 1010 FORMAT(/,8X,10HHORIZONTAL,85X,8HVERTICAL)
      END
C
      SUBROUTINE SPLSA2(SOL,IXY,N)
C  THIS SUB. PLOTS FOR SOLUTION SOL
C  LSA2 DIAGRAM
C  IXY IS A WORKING AREA
C  THE INPUT COORDINATES SOL(.,1) (FIRST AXIS)
C  AND SOL(.,J) (SECOND AXIS) HAVE TO LIE
C  BETWEEN 0. AND 100.
C
      IMPLICIT INTEGER*2  (I-N)
      INTEGER*2 IXY(N,2)
      INTEGER*2 IW(25), IBLANK, ITEMP
      LOGICAL   TMPOPEN
C
      CHARACTER*4 FOR(30)
      COMMON/LAB/LABEL (26)
      CHARACTER*40 LABEL
      COMMON/SCHARS/UP,BOTTM,LEFT,RIGHT,
     $               UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT,FORMFEED
      CHARACTER*1 UP,BOTTM,LEFT,RIGHT,
     $             UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT,FORMFEED
      CHARACTER*4 F1,F56,F57,F3,F55,FALF,FINT
C
CY      DIMENSION SOL(N,1)
      DIMENSION SOL(N,2)
      COMMON/IO/NWRITE,NSCR
      DATA F1,F56,F57,F3,F55/4H(1X, ,4HI3,1 ,4HX,A) ,4HA1  ,4H,A1, /
      DATA FALF,FINT/ 4H,A2 ,4H,I2 /
      DATA IBLANK / 2H     /
C      DATA I100,I50,I0,IBL/5H100.. ,5H 50..  ,5H  0.. ,5H        /
C     DATA UP,BOTTM,LEFT,RIGHT/'Í','Í','º','º'/
C     DATA UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT / 'É', '»', 'È', '¼' /
C
C  THIS PLOT IS AVAILABLE FOR 51
C  POSITIONS IN EACH AXIS SINCE THE
C  COORDINATES GO FROM 0./2 TO 100./2
C
C     FIRSTLY, OPEN THE SCREEN FILE
C
      ITEMP = 21
      OPEN (ITEMP, FILE='LSA2.SCR', ACCESS='SEQUENTIAL',
     $             FORM='FORMATTED', ERR=100)
      TMPOPEN = .TRUE.
      GOTO 101
 100  TMPOPEN = .FALSE.
      WRITE (*,*) ' ** Cannot open screen plot file **'
 101  CONTINUE
C
      NCOOR=24
      NROW=NCOOR-1
      NCOL=NCOOR+1
      NCOL2=NCOL+NCOL
      NFOR=2+3+NCOL
C
C  CLEAR FORMAT ARRAY
C      DO 1 K=1,NFOR
C         FOR(K)=BLANK
C    1 CONTINUE
      FOR(1)=F1
      FOR(2)=F3
      FOR(NFOR-2)=F55
      FOR(NFOR-1)=F56
      FOR(NFOR)=F57
C
C  POINT COORDINATES ON FIRST AXIS
      DO 3 K=1,N
         IXY(K,1)=.24*SOL(K,1)+1.5
    3 CONTINUE
C
C  POINT COORDINATES ON SECOND AXIS
      DO 71 K=1,N
         LL=.22*SOL(K,2)+1.5
         IXY(K,2)=NROW+1-LL
   71 CONTINUE
      WRITE (*,*) CHAR (7), CHAR (7)
C      WRITE(*,72)
C   72 FORMAT(/)
      WRITE(*,1001) UPLEFT, (UP,JJ=1,NCOL2), UPRIGHT,
     $                                  '       LSA2 SOLUTION'
      IF (TMPOPEN) WRITE(ITEMP,1001) UPLEFT, (UP,JJ=1,NCOL2), UPRIGHT,
     $                                  '       LSA2 SOLUTION'
C
      DO 18 KY=1,NROW
         DO 7 K=1,NCOL
            IW(K)=IBLANK
            FOR(K+2)=FALF
    7    CONTINUE
         DO 9 KJ=1,N
            IF(IXY(KJ,2).NE.KY) GO TO 9
            K=IXY(KJ,1)
            FOR(K+2)=FINT
            IW(K)=KJ
    9    CONTINUE
C
       IF (KY .LE. N) THEN
          WRITE (*,FOR) LEFT, IW, RIGHT, KY, LABEL (KY) (1:23)
          IF (TMPOPEN) WRITE(ITEMP,FOR) LEFT, IW, RIGHT,
     $                                  KY, LABEL (KY) (1:23)
       ELSE
          WRITE (*,FOR) LEFT, IW, RIGHT
          IF (TMPOPEN) WRITE(ITEMP,FOR) LEFT, IW, RIGHT
       END IF
C
   18 CONTINUE
      WRITE(*,1001) BOTTMLEFT, (BOTTM,JJ=1,NCOL2), BOTTMRIGHT
      IF (TMPOPEN) WRITE(ITEMP,1001) BOTTMLEFT, (BOTTM,JJ=1,NCOL2),
     $                               BOTTMRIGHT
C
      CLOSE (ITEMP)
      RETURN
C
C  THE FOLLOWING FORMATS ARE
C  AVAILABLE FOR 51 POSITIONS
 1001 FORMAT(1X,104A)
C
      END
C
      SUBROUTINE OPENFILE
      IMPLICIT INTEGER*2  (I-N)
      COMMON/IO/NWRITE,NSCR
C
      CHARACTER*128 PARS
CY      INTEGER*2 I, STATUS
      INTEGER*2 I
CY      INTEGER*4 NARGS
      INTEGER*4 IARGC
C
      NWRITE=3
      NSCR=9
CY      I = NARGS () - 1
      I = IARGC()
C
      IF (I .LT. 1) GOTO 800
C
      I = 1
CY      CALL GETARG (I, PARS, STATUS)
CY      IF (STATUS .LE. 0)  GOTO 810
      CALL GETARG (I, PARS)
      IF (PARS(1:1).EQ.' ')  GOTO 810
C
      OPEN (NWRITE,FILE=PARS,ACCESS='SEQUENTIAL',
     $              FORM='FORMATTED',ERR=820 )
C
      OPEN (NSCR,FILE='FORT9',STATUS='OLD',ACCESS='SEQUENTIAL',
     $              FORM='UNFORMATTED' )
      REWIND NSCR
C
      RETURN
C
 800  WRITE (*,900) CHAR(7)
      GOTO 850
C
 810  WRITE (*,920) CHAR(7)
      GOTO 850
C
CY 820  WRITE (*,910) PARS (1:STATUS), CHAR(7)
 820  WRITE (*,910) PARS, CHAR(7)
      GOTO 850
C
 850  STOP 2
C
 900  FORMAT (' *** ERROR ***  No parameter supplied.', A1)
 910  FORMAT (' *** ERROR *** opening file ', A, A1)
 920  FORMAT (' *** ERROR *** Parameter status error in  GETARG.', A1)
C
      END

