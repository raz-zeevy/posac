
      PROGRAM NWPOSA

INCLUDE 'MPARMLIN'

C     PARAMETER (IBLOCK=20000)
C
C
C***********************************************************************
C
C  TWO-DIMENSIONAL PARTIAL ORDER SCALOGRAM ANALYSIS
C         WITH BASE COORDINATES (POSAC)
C
C  A VERSION WITH EXTERNAL VARIABLES
C
C***********************************************************************
C
C                 PURPOSE
C
C    GIVEN P , A PARTIALLY  ORDERED SET OF PROFILES
C  IN NV VARIABLES , THIS PROGRAM TRIES TO REDUCE
C  THE NUMBER OF VARIABLES FROM NV TO 2 .
C    MORE PRECISELY , TO EACH PROFILE (C1,C2,...,CNV)
C  IN P WILL CORRESPOND A PROFILE (X,Y) IN R**2
C  SUCH THAT , THROUGH THIS APPLICATION , THE PARTIAL
C  ORDER IN P IS PRESERVED AS WELL AS POSSIBLE .
C    X AND Y ARE CALLED BASE COORDINATES .
C
C    FURTHERMORE , FOR SPECIFIED CATEGORIES OF A GIVEN
C  EXTERNAL VARIABLE (A VARIABLE NOT IN THE ORIGINAL SET
C  OF THE NV VARIABLES) REPRESENTING AN EXTERNAL CRITERION
C  OR TRAIT , TRAIT-DIAGRAMS ARE PRESENTED DEPICTING THE
C  PROPORTION OF SUBJECTS POSSESSING THAT TRAIT AMONG
C  ALL THOSE SHARING THE SAME PROFILE IN NV VARIABLES .
C    THE TRAIT MAY BE SPECIFIED ALSO AS A COMBINATION
C  (INTERSECTION) OF RESPONSE CATEGORIES FROM DIFFERENT
C  EXTERNAL VARIABLES .
C
C***********************************************************************
C
C         DESCRIPTION OF THE RESULTS
C
C - SOME INFORMATION ON THE INPUT DIRECTIVES AND PARAMETERS
C - A LIST OF THE COMPUTED PROFILES WITH THEIR COMPARABILITY RELATIONS
C - TWO COEFFICIENTS 'CORREP' AND 'SCODIS' , WHICH SHOW THE DEGREE OF
C   FIT BETWEEN THE DATA AND THE COORDINATES COMPUTED BY POSAC.
C - A LIST CONTAINING THE COMPUTED COORDINATES OF EACH PROFILE.
C - COEFICIENTS OF WEAK MONOTONICITY BETWEN EACH OBSERVED ITEM
C   AND THE FACTORS : J,L,X,Y,P AND Q
C - A TWO-DIMENSIONAL PLOT OF THE SCALOGRAM.
C - A DIAGRAM FOR EACH ITEM (VARIABLE).
C - OPTIONALLY , TWO TRAIT-DIAGRAM (ABSOLUTE AND RELATIVE) FOR EACH
C - EXTERNAL CRITERION.
C
C***********************************************************************
C
C                 LIMITATIONS
C
C  1. MAXIMUM NO. OF INTERNAL VARIABLES IS 20   (INTMAX).
C
C  2. VALUE OF EACH VARIABLE SHOULD BE BETWEEN 1 AND 99  .
C
C  3. MAXIMUM LENGTH OF CASE ID IS 10 CHARACTERS (IDLEN).
C
C  4. MAXIMUM NO. OF CASE ID'S IS 500  (IDMAX).
C
C  5. MAXIMUM LENGTH OF INTERNAL VARIABLE LABELS IS 40 CHARACTERS (LABLEN).
C
C  6. MAXIMUM NUMBER OF INTERVALS FOR EXTERNAL VARIABLES IS 10 (MAXINTRV).
C
C  7. MAXIMUM NUMBER OF INTERVALS FOR DEFINING EXTERNAL TRAIT IS 10 (MAXINTRV).
C
C  8. MAXIMUM LENGTH OF EXTERNAL TRAIT LABELS IS 40 CHARACTERS (LABLEN).
C
C  9. MAXIMUM NUMBER OF EXTERNAL MAPS IS 30 (MAXEXTMP).
C
C 10. MAXIMUM LENGTH OF INPUT FORMAT IS 100 CHARACTERS (LENIFOR).
C
C 11. MAXIMUM NUMBER OF DIMENSIONS IS 2 (M).
C
C***********************************************************************
C
C                 PROCEDURE CARD
C
C      A.  TITLE CARD (COLUMNS 1 TO 80)
C
C      B.  PARAMETER CARD IN FORMAT 20I4 CONTAINING THE FOLLOWING
C          INFORMATION
C           1)  NV -
C                NUMBER OF VARIABLES (INCLUDING THE EXTERNALS)
C           2)  IDATA -
C                IF IDATA=0 OR BLANK DATA ARE SUBJECTS
C                IF IDATA.NE.0        DATA ARE PROFILES.
C           3)  LOWFRQ -
C                IF LOWFRQ=0 OR BLANK NO REJECTING PROFILES
C                IF LOWFRQ.NE.0 REJECTING PROFILES WHOSE
C                         FREQUENCY IS  .LE.LOWFRQ.
C           4)  MISSNG
C                IF MISSNG=0 OR BLANK  0 IS MISSING VALUE
C                IF MISSNG.NE.0    MINIMAL AND MAXIMAL CATEGORIES
C                   HAVE TO BE SPECIFIED  (SEE LINE D. BELOW)
C           5)  IPOWER
C                IF IPOWER=0 OR BLANK POWER OF THE BALANCING
C                  WEIGHTS ARE : ND1=4 FOR INCOMPARABLES
C                          ND2=4 FOR   COMPARABLES
C                IF IPOWER.NE.0  ND1 AND ND2 HAVE TO BE
C                          SPECIFIED (SEE LINE E. BELOW)
C           6)  ITEMDGPLT
C                IF ITEMDGPLT = 0 OR BLANK, ITEM DIAGRAMS ARE NOT 
C                                           PLOTTED
C                IF ITEMDGPLT = 1, ITEM DIAGRAMS ARE PLOTTED.
C           7)  NLAB -
C                NUMBER OF VARIABLE LABELS . (SEE LINE F. BELOW)
C           8)  NXT  NUMBER OF EXTERNAL VARIABLES. (SEE LINE G. BELOW)
C           9)  MAP  NUMBER OF EXTERNAL TRAIT MAPS. (SEE LINE H. BELOW)
C          10)  IEXTDIAG -
C                IF IEXTDIAG=0 OR BLANK DO NOT PRINT EXTERNAL DIAGRAMS
C                IF IEXTDIAG.NE.0 PRINT EXTERNAL DIAGRAMS
C          11)  ITABLE
C                IF ITABLE=0 OR BLANK  COMPLETE PROCESS
C                IF ITABLE.NE.0  ONLY LIST OF PROFILES
C                          WITH SOME INFORMATION.
C          12)  INITX
C                IF INITX=0 OR BLANK  FIRST APPROXIMATION
C                        COMPUTED BY THE PROGRAM
C                IF INITX.NE.0  FIRST APPROXIMATION GIVEN
C                         BY THE USER  (SEE LINE I. BELOW)
C          13)  IBOXSTRNG -
C                IF IBOXSTRNG=0 OR BLANK USE DEFAULT GRAPHICS CHARACTERS
C                IF IBOXSTRNG.NE.0 SUPPLY USER GRAPHICS CHARACTERS
C                (SEE LINE J. BELOW)
C          14)  IFF -
C                IF IFF=0 OR BLANK USE DEFAULT FORM-FEED CHARACTER  CHAR(12)
C                IF IFF.NE.0 SUPPLY OTHER FORM-FEED CHARACTER
C                (SEE LINE K. BELOW)
C          15)  ITRM
C                ITRM IS THE MAXIMUM NUMBER OF ITERATIONS TO BE USED.
C                IF ITRM IS 0 OR BLANK, A DEFAULT VALUE OF 15 IS USED.
C          16)  IWRTFLS
C                IF IWRTFLS = 0, THE 4 ASCII OUTPUT FILES ARE NOT WRITTEN.
C                IF IWRTFLS > 0, THE 4 ASCII OUTPUT FILES WITH EXTENSION .PSC
C                                ARE WRITTEN.
C          17)  IFSHMR
C                IF IFSHMR = 0, PROGRAM SHEMOR IS NOT TO BE RUN OR IS TO BE
C                               RUN USING DEFAULT RECODING CATEGORIES.
C                IF IFSHMR = 1, PROGRAM SHEMOR IS TO BE RUN TAKING 
C                               DIRECTIVES FROM THE END OF THIS FILE
C                               FOLLOWING LINE CONTAINING THE WORD SHEMOR
C                               IN COLUMNS 1-6 OF A LINE FOLLOWING ALL
C                               INFORMATION NEEDED FOR POSAC.
C                (SEE LINE L. BELOW)
C          18)  IFRQONE -
C                IF IFRQONE = 0 OR BLANK, ACTUAL FREQUENCIES ARE USED.
C                IF IFRQONE = 1,  FREQUENCIES ARE ALL SET TO 1 (IF NOT
C                                          REJECTED WHERE LOWFRQ IS SET).
C
C
C      C.  INPUT FORMAT FOR DATA  (IN I-FORMAT)
C                IF DATA ARE PROFILES THE FIRST SPECIFICATION
C                MUST BE FOR THE PROFILE FREQUENCY
C                IF DATA CONTAINS CASE ID IT SHOULD APPEAR AS THE FIRST
C                ITEM IN THE FORMAT AND SHOULD BE IN A-FORMAT.
C
C     D1.  (REQUIRED ONLY IF MISSNG.NE.0)
C          MINIMAL CATEGORY FOR EACH VARIABLE IN FORMAT 20I4
C
C     D2.  (REQUIRED ONLY IF MISSNG.NE.0)
C          MAXIMAL CATEGORY FOR EACH VARIABLE IN FORMAT 20I4
C
C      E.  (REQUIRED ONLY IF IPOWER.NE.0)
C          ND1,ND2  IN FORMAT 2I4
C           ND1 BALANCING WEIGHT POWER FOR INCOMPARABLES 0.LE.ND1.LE.4
C           ND2 BALANCING WEIGHT POWER FOR   COMPARABLES 0.LE.ND2.LE.4
C
C      F.  (REQUIRED ONLY IF NLAB.NE.0)
C          VARIABLE LABELS - A NUMBER (NLAB ABOVE) OF LINES IN THE FOLLOWING
C           FORMAT : COL.  1- 4  -  VARIABLE NO.
C                COL. 11-50  -  VARIABLE LABEL.
C
C      G.  (REQUIRED ONLY IF NXT.NE.0)
C          HERE INSERT NXT CARDS TO DEFINE THE ADMISSIBLE CATEGORIES
C           OF EACH EXTERNAL VARIABLE . THESE CATEGORIES MAY BE
C           DEFINED AS A UNION OF SEVERAL INTERVALS
C          FOR EACH CARD GIVE THE FOLLOWING INFORMATION IN FORMAT 20I4
C          SERIAL NUMBER OF THE EXTERNAL VARIABLE,NUMBER OF INTERVALS,
C           INTERVAL1,INTERVAL2,.....
C         (EACH INTERVAL IS COMPOSED BY TWO NUMBERS . A SINGLE
C          CATEGORY CAN BE DEFINED BY TWO EQUAL NUMBERS)
C         THE NUMBER OF INTERVALS IS LIMITED TO 10
C
C      H.  (REQUIRED ONLY IF MAP.NE.0)
C          HERE INSERT MAP*(NXT+1) CARDS
C          FOR EACH MAP GIVE :
C          -A LABEL CARD TO IDENTIFY THE MAP (UP TO 40 CHARACTERS)
C          -NXT CARDS WHERE IN EACH CARD ARE DEFINED THE CATEGORIES
C            DEFINING THE TRAIT IN THIS EXTERNAL VARIABLE
C           THE FORMAT IS AS IN G.  20I4
C          SERIAL NUMBER OF THE EXTERNAL VARIABLE,NUMBER OF INTERVALS,
C          INTERVAL1,INTERVAL2,.....
C
C
C     I1.  (REQUIRED ONLY IF INITX.NE.0)
C          INPUT FORMAT FOR INITIAL APPROXIMATION
C
C     I2.  (REQUIRED ONLY IF INITX.NE.0)
C          INITIAL APPROXIMATION
C         X AND Y COORDINATES FOR EACH PROFILE
C                       ACCORDING TO THE INPUT FORMAT 9A.
C
C      J.  (REQUIRED ONLY IF IBOXSTRNG.NE.0)
C          HERE INSERT ONE LINE OF 8 CHARACTERS FOR PRINTING DIAGRAMS' FRAMES
C          IN THE FOLLOWING ORDER :TOP,BOTTOM,LEFT,RIGHT
C                        TOPLEFT,TOPRIGHT,BOTTOMLEFT,BOTTOMRIGHT
C
C      K.  (REQUIRED ONLY IF IFF.NE.0)
C          HERE INSERT ONE LINE OF ONE CHARACTER WHICH WILL SUBSTITUTE ALL
C          DEFAULT FORM-FEED CHARACTERS (CHAR(12)) IN OUTPUT PRODUCED
C          BY PROGRAM.
C
C
C      L.  (DIRECTIVES FOR PROGRAM SHEMOR.  REQUIRED ONLY IF IFSHMR IS NOT 0).
C          THE FIRST LINE CONTAINS THE WORD SHEMOR IN COLUMNS 1-6.
C          THE FIRST SENTENCE INDICATES THE RECORD LENGTH OF RAW DATA 'OLDDAT'
C          FOR EXAMPLE :
C               RECORD LENGTH   120
C          IF SUCH A SENTENCE IS ABSENT THE DEFAULT IS 80
C
C          THE FOLLOWING SENTENCES INDICATE THE RECODING OF THE ORIGINAL
C          COORDINATES  X Y J L  BEFORE THEY ARE ADDED TO RAW DATA
C          THE TYPE OF SUCH A SENTENCE IS :
C
C          FOR V1,V2,...  RECODE  N11 THRU  N12 = L1, N21 THRU N22=L2 ,
C          N31 THRU N32 =L3 , .........   , NM1 THRU NM2 = LM .
C
C          WHERE :
C            THE VARIABLE LIST V1,V2,... IS THE LIST X,Y,J,L OR A PART OF IT
C            THE NUMBERS NI1,NI2,LI (I=1,...,M) ARE INTEGER NUMBERS
C          FOR EXAMPLE :
C
C          FOR X,Y RECODE  0 THRU 45 = 1, 46 THRU 75 =2, 76 THRU 100= 3.
C          FOR J RECODE 0 THRU 60 =1 , 61 THRU 110 =2,111 THRU 150 = 3,
C                      151 THRU 200 = 4 .
C          FOR L  RECODE 0 THRU 100 =1 , 101 THRU 200 = 2 .
C
C          HERE ALSO THERE ARE DEFAULTS :
C
C          FOR X,Y RECODE   0 THRU  25 = 1 ,  26 THRU  50 = 2 ,
C                          51 THRU  75 = 3 ,  76 THRU 100 = 4 .
C
C          FOR J,L RECODE   0 THRU  50 = 1 ,  51 THRU 100 = 2 ,
C                         101 THRU 150 = 3 , 151 THRU 200 = 4 .
C
C***********************************************************************
C
C                REFERENCES
C
C
C  COOMBS,C.H.,1964. A THEORY OF DATA. NEW YORK,WILEY(CHAP. 12)
C
C  GUTTMAN,L. 1972. A PARTIAL ORDER SCALOGRAM CLASSIFICATION OF
C      PROJECTIVES TECHNIQUES. IN M. HAMMER, K. SALZINGER, AND
C      S. SUTTON(EDS.) PSYCHOPATHOLOGY, NEW YORK,WILEY
C
C  SHYE,S. 1978. PARTIAL ORDER SCALOGRAM ANALYSIS ,IN S. SHYE (ED.)
C      THEORY CONSTRUCTION AND DATA ANALYSIS IN THE BEHAVIORAL
C      SCIENCES. SAN FRANCISCO , JOSSEY BASS.
C
C  AMAR,R. 1980. MATHEMATICAL DESCRIPTION OF POSAC/LSA PROGRAM
C      ALGORITHM. IN S.SHYE PARTIAL ORDER SCALOGRAM ANALYSIS
C      OF PROFILES AND A RELATED LATTICE ANALYSIS OF THE ITEMS
C      BY THEIR SCALOGRAM GENERIC ROLES . THE ISRAEL INSTITUTE
C      OF APPLIED SOCIAL RESEARCH  PUBL. SS/789/E
C
C  SHYE,S. 1981. AN INTEGRATED METHOD FOR SCALING SUBJECTS AND
C      STRUCTURING THEIR MULTIVARIATE ATTRIBUTES , DESCRIPTION
C      AND ILLUSTRATION OF PARTIAL ORDER SCALOGRAM AND LATTICE
C      ANALYSIS. THE ISRAEL INSTITUTE OF APPLIED SOCIAL RESEARCH
C      PUBL. SS/837/E
C
C********************************************************************
C
C  PROGRAM WRITTEN IN FORTRAN 77 BY REUVEN AMAR FROM
C  THE ISRAEL INSTITUTE OF APPLIED SOCIAL REASEARCH JERUSALEM
C
C **********************************************************************
C **********************************************************************
C
C     EXAMPLE OF INPUT DIRECTIVES FOR PROGRAMS 'POSAC' AND 'SHEMOR'
C     -------------------------------------------------------------
C
C //////////////////////// FOR 'POSAC' /////////////////////////////////
C / POSAC ON 6 VARIABLES (ITEMS)
C /   6
C /(91X,4I1,2X,2I1,/)
C //////////////////////// FOR 'SHEMOR' ////////////////////////////////
C / RECORD LENGTH 128
C / FOR X,Y RECODE 0 THRU 11 =1,12 THRU 22=2,23 THRU 33=3,34 THRU 44=4,
C /          45 THRU 55=5,56 THRU 66=6,67 THRU 77=7,78 THRU 88=8,
C /          89 THRU 100=9 .
C / FOR J,L RECODE 0 THRU 22 =1,23 THRU 44=2,45 THRU 66=3,67 THRU 88=4,
C /          89 THRU 110=5,111 THRU 132=6,133 THRU 154=7,
C /          155 THRU 176=8,177 THRU 200=9 .
C /----------- END OF RECORD ---------(END OF 'SHEMOR' DIRECTIVES)
C ///////////////// FOR 'POSAC' (WITH EXTERNAL VARIABLES) //////////////
C / POSAC ON 7 INT. VAR. AND 2 EXT. VAR. WITH 4 EXT. MAPS
C /   9   0   0   0   0   0   2   4
C /(104X,7I1,/,T6,I1,T15,I1)
C /   8   1   0   9
C /   9   1   0   9
C /MALES UNDER 40 Y. OF AGE
C /   8   1   1   1
C /   9   1   1   4
C /MALES OVER 40 Y. OF AGE
C /   8   1   1   1
C /   9   1   5   9
C /FEM. UNDER 40 Y. OF AGE
C /   8   1   2   2
C /   9   1   1   4
C /FEM. OVER 40 Y. OF AGE
C /   8   1   2   2
C /   9   1   5   9
C //////////////////////////////////////////////////////////////////////
C***********************************************************************
C     COMMON X(IBLOCK)
C
INCLUDE 'MCOMNBLK'
C
      CALL MAINSUB
      STOP 'POSAC Completed'
      END


      SUBROUTINE MAINSUB

INCLUDE 'MPARMLIN'

      PARAMETER (IDMAX=500)
      PARAMETER (LENIFOR=255)
      PARAMETER (LABLEN=40)
      PARAMETER (IDLEN=10)
      PARAMETER (MITR=15)
C
C     COMMON X(1)

INCLUDE 'MCOMNBLK'

      COMMON/IO/NTAPE,NREAD,NWRITE,NSCR
      COMMON/SHEMOR/INDIC,IFSHMR
      COMMON/AA/NV,NCAS,NXT,MAP,M
      COMMON/BB/IFOR
      COMMON/CC/ISMN,ISMX,IEND
C      COMMON/DIM/M,N1,N2,N3,N4,N5,N6,N7,N8,N9,N10,N11,N12
C      COMMON/DIM3/N210,N220,N230,N240
      COMMON/OPT/IDATA
      COMMON/OPMISS/MISSNG
      COMMON/SBPOSA/INITX,FND1,FND2
      COMMON/VARINT/INTERN
      COMMON/ABTOL/A0,B0,TOL,EXTRA
      COMMON/REJECT/ LOWFRQ, IFRQONE
      COMMON/KENID/YESID
      COMMON/ZIHOUY/ID
      COMMON/LAB/LABEL (MNV),PATH
      COMMON/FF/FORMFEED
      COMMON/BOXSTRNG/UP,BOTTM,LEFT,RIGHT,
     $               UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT
      COMMON/PARAM/IEXDIAGRM, ITEMDGPLT, IWRTFLS
      COMMON/NIT/ITRM
      COMMON   /WCHAR/ PARS, DUMMY
C
      CHARACTER*48 PARS
      CHARACTER*192 DUMMY (2)
      CHARACTER*1 UP,BOTTM,LEFT,RIGHT,
     $             UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT
      CHARACTER*1 FORMFEED
      CHARACTER*40 LABEL
      CHARACTER*80 PATH
      CHARACTER*10 ID(IDMAX)
      CHARACTER IFOR*(LENIFOR)
      CHARACTER WORD*3, LWORD*14
C
      INTEGER*2 NV,NCAS,NXT,MAP,M,ISMN,ISMX,IEND
      LOGICAL YESID
C
      INTEGER*2 IVAR,J,NCAS2
C
C **********************************************************************
C
      CALL INITIAL
      CALL OPENFIL
C
C **********************************************************************
C
      WRITE (NWRITE,1004)
      WRITE (NWRITE,1041)
C  READING THE TITLE
      READ (NREAD,'(A)')  IFOR
C  READING THE PARAMETERS
      READ (NREAD,1001)  NV, IDATA, LOWFRQ, MISSNG, IPOWER,
     $                   ITEMDGPLT, NLAB, NXT, MAP, IEXDIAGRM, ITABLE,
     $                   INITX, IBOXSTRNG, IFF, ITRM, IWRTFLS, IFSHMR,
     $                   IFRQONE
C
      IF (NV .GT. MNV) THEN
          WRITE (*,2010) NV, 'variables', MNV
          WRITE (NWRITE,2010) NV, 'variables', MNV
          STOP 2
      END IF
C
      IF (NXT .GT. MNXT) THEN
          WRITE (*,2010) NXT, 'external variables', MNXT
          WRITE (NWRITE,2010) NXT, 'external variables', MNXT
          STOP 2
      END IF
C
      IF (MAP .GT. MMAP) THEN
          WRITE (*,2010) MAP, 'maps', MMAP
          WRITE (NWRITE,2010) MAP, 'maps', MMAP
          STOP 2
      END IF
C
C
C  ITRM IS THE MAX NUMBER OF GLOBAL ITERATIONS
C
      IF (ITRM .EQ. 0) ITRM = MITR
C
C       IWRTFLS IS CHANNEL NUMBER FOR AUXILIARY FILES, IF USED
C
      IF (IWRTFLS .GT. 0) IWRTFLS = 12
C
      INTERN=NV-NXT
      IF (INTERN .GT. INTMAX) THEN
          WRITE (*,2010) INTERN, 'internal variables', INTMAX
          WRITE (NWRITE,2010) INTERN, 'internal variables', INTMAX
C          PRINT *,'ERROR - TOO MANY INTERNAL VARIABLES'
C          PRINT *,'THIS PROGRAM CAN HANDLE UP TO ', INTMAX,
C     $            ' INTERNAL VARIABLES'
          STOP 2
      ENDIF
C
C **********************************************************************
C
C  WRITING OUT PARAMETERS AND OPTIONS SELECTED BY USER
C
      IF (IFRQONE .EQ. 1) THEN
          LWORD = 'STRUCTURAL'
      ELSE IF (ITABLE .NE. 0) THEN
          LWORD = 'PROFILES'
      ELSE
          LWORD = 'DISTRIBUTIONAL'
      END IF
      WRITE (NWRITE,1003)  IFOR, NV, PARS, LWORD
C
      WORD='NO '
      IF (ITEMDGPLT.NE.0) WORD='YES'
      WRITE (NWRITE,2003) WORD
      WORD='NO '
      IF (IEXDIAGRM.NE.0) WORD='YES'
      WRITE (NWRITE,2001) WORD
C
      WRITE (NWRITE,2002) MITR,ITRM
C
C **********************************************************************
C
C  READING THE FORMAT OF DATA
C
      READ (NREAD,'(A)')   IFOR
C
C  LOOK IF SPECIFICATION A IS GIVEN IN THE FORMAT FOR ID
C
      CALL WHATFR(IFOR,YESID)
C
      J = LENIFOR
 50   IF (IFOR (J:J) .EQ. ' ') THEN
          J = J - 1
          IF (J .GT. 1) GOTO 50
      END IF
C
      IF (J .LE. 102) THEN
          WRITE (NWRITE,1002)  IFOR (1:J)
      ELSE
          WRITE (NWRITE,1002)  IFOR (1:102)
          IF (J. LE. 204) THEN
               WRITE (NWRITE,1012)  IFOR (102:J)
          ELSE
               WRITE (NWRITE,1012)  IFOR (102:204)
               WRITE (NWRITE,1012)  IFOR (204:J)
          END IF
      END IF
C
C **********************************************************************
C
      REWIND INDIC
      WRITE(INDIC) INT2(NV),INT2(NTAPE),INT2(IDATA),INT2(NXT),
     $             INT2(ITABLE),INT2(INTERN),INT2(IFSHMR)
      WRITE(INDIC) IFOR,YESID
C
C      N1=NV+1
C      N2=N1+NV
C      N20=N2+NV
C      N21=N20+NV
C      N22=N21+NXT
C      N23=N22+NXT
C      N24=N23+NXT*10
C      N210=N24+10*NXT
C      N220=N210+NXT*MAP
C      N230=N220+NXT*MAP
C      N240=N230+10*NXT*MAP
C      N2BIS=N240+10*NXT*MAP
C      N2TER=N2BIS+NV
C
C **********************************************************************
C
      CALL MISS(XN2BIS,XN2TER,NV)
      IF (IPOWER.NE.0) READ (NREAD,*) FND1,FND2
C
      IF (NLAB .NE. 0)  READ (NREAD,1007) (IVAR,LABEL(IVAR),J=1,NLAB)
C
      IF(NXT.NE.0)   CALL EXTVAR(XN21,XN22,XN23,XN24)
C
      CALL OUTVAR (XN21,XN22,XN23,XN24, NLAB)
C
      IF(MAP.NE.0)   CALL EXTMAP(XN210,XN220,XN230,XN240,NXT,MAP)
C
      CALL READAT(XX1,XN1,XN2,XN2BIS,XN2TER,XN21,XN22,XN23,XN24)
C
      IF(NCAS.LT.3) THEN
CRRRRRRRRRRRR
	      WRITE (*, 6000) NCAS
 6000		FORMAT (" NCAS = ", I4)
          WRITE(*,1005)
          WRITE(NWRITE,1005)
          STOP 2
      END IF
C
      WRITE (*,*) ' There are ', NCAS, ' cases to analyse.'
      IF (NCAS .GT. MNC) THEN
          WRITE (*,2010) NCAS, 'cases', MNC
          WRITE (NWRITE,2010) NCAS, 'cases', MNC
          STOP 2
      END IF
C
C
C **********************************************************************
C
      IF (IBOXSTRNG .NE. 0) READ (NREAD,1006) UP,BOTTM,LEFT,RIGHT,
     $                        UPLEFT,UPRIGHT,BOTTMLEFT,BOTTMRIGHT
      IF (IFF .NE. 0) READ (NREAD,1006) FORMFEED
C
C **********************************************************************
C
C  FOR MEMORY WE HAVE TO ADD 2 TO NCAS BECAUSE PERHAPS
C  THE MAXIMAL AND MINIMAL PROFILES DON"T EXIST
C      NC=NCAS+2
C      N3=N2BIS
C      N4=N3+NC
C      N5=N4+NC*NV
C      N6=N5+NC
C      N7=N6+NC
C
C    *********
C
      CALL PERMUT(XX1,XN3,XN4,XN1,XN2,XN21,XN20,NV,NCAS,NXT)
C
      IF (NXT .NE. 0 .AND. MAP .NE. 0)
     $         CALL ORDEXT(XN21,XN22,XN23,XN24,
     $                     XN210,XN220,XN230,XN240,NXT,MAP,NV)
C
      CALL POFREQ(XN3,XN4,XN5,XN6,NCAS,NV)
C
      IF(LOWFRQ.GT.0) CALL PRFOUT(XN3,XN4,XN5,XN6,NCAS,NV)
C
C     IF (IFRQONE .EQ. 1)  CALL ONEFRQ (XN3, NCAS)
C
      NCAS2=NCAS

      CALL ADDMNX(XN1,XN2,XN3,XN4,XN5,NV,NCAS2)
C
C ********* PROCESS OF LABELS ************

C  SIGNIFICANT ONLY WHEN SUBJECTS (INSTEAD OF PROFILES) ARE PROCESSED
C  THAT IS WHEN IDATA = 0
C
      IF (NCAS2 .LE. IDMAX .AND. IDATA .EQ. 0) THEN
C
          CALL DECRES(XN6,XN3,XN4,XN5,NCAS2,NV)
          CALL PRNTID(XN6,XN3,XN4,XN5,NV,NCAS2)
C
      ENDIF
C
C ********************************
C
      CALL SCORD(XX1,XN3,XN4,XN5,NCAS2,NV)
C
C
      CALL COMPAR(XN3,XN4,XN5,XN6,NV,IEND,NCAS2)
C   **********
C
      IF (ITABLE.NE.0) STOP 2
      IF (IEND.LT.3) THEN
          WRITE (*,1005)
          WRITE (NWRITE,1005)
          STOP 2
      END IF
C
      WRITE (*,*) ' There are ', IEND, ' distinct profiles.'
      IF (IEND .GT. MIEND) THEN
          WRITE (*,2010) IEND, 'distinct profiles', MIEND
          WRITE (NWRITE,2010) IEND, 'distinct profiles', MIEND
          STOP 2
      END IF
C
      IF (IFRQONE .EQ. 1)  CALL ONEFRQ (XN3, NCAS2)
C
C **********************************************************************
C
C      N8=N7+IEND*IEND
C      N9=N8+IEND*M
C      N10=N9+IEND*M
C      N11=N10+IEND*M
C      N12=N11+IEND*M
C
C
      CALL POSAC
      CALL CLOSEFL
C
C      WRITE (*,*) CHAR (7), CHAR (7)
      RETURN
C
 1001 FORMAT(20I4)
 1003 FORMAT(17H NAME OF THE JOB ,13(1H.),A49,/
     1         21H NUMBER OF VARIABLES ,9(1H.),I4,/
     2         12H INPUT FILE ,18(1H.),A48/
     3         11H PROCEDURE ,19(1H.),A)
 1002 FORMAT(21H THE INPUT FORMAT IS ,9(1H.),A)
 1012 FORMAT(21X,9(1H.),A)
C 1004 FORMAT(/////,48X,41(1H*),/,48X,25H*TWO-DIMENSIONAL PARTIAL ,
C     $16HORDER SCALOGRAM*,/,48X,1H*,4X,19HANALYSIS WITH BASE ,
C     $11HCOORDINATES,5X,1H*,/,48X,1H*,16X,6HPOSAC1,17X,1H*,
C     $/,48X,41(1H*),//,44X,31HTHE ISRAEL INSTITUTE OF APPLIED,
C     $16H SOCIAL RESEARCH,/,53X,
C     $28HP.O.B. 7150 JERUSALEM ISRAEL,//)
 1004 FORMAT (/////// 52X,'X   P   D   A' //////32X,55(1HÍ)/
     $   32X,'E X P E R T   D a t a   A n a l y s i s   S Y S T E M S'/
     $   32X,55(1HÍ) /
     $        / 47X,'Multiple Scaling/AI R&D Project'///)
C  1004   FORMAT(////////////32X,67(1H=)/
C     $      32X,'D A T A   P R O C E S S I N G   &',
C     $   '   R E S E A R C H   M E T H O D S'/32X,67(1H=) )
 1041 FORMAT(/////57X,'S T R U C T U R A L'//
     $            58X,'P O S A C / L S A'/58X,17(1H-)/
     $    43X,'Partial Order Scalogram Analysis by Coordinates'/
     $    55X,'& Lattice Space Analysis'//59X,'Version 2.0 1988'////)
 1005 FORMAT(//,13H  ***ERROR***,26H  THE NUMBER OF DIFFERENT ,
     $38HPROFILES MUST BE AT LEAST EQUAL TO 3 .,/,10H  PERHAPS ,
     $39HYOU HAVE AN ERROR IN THE OPTION MISSNG ,
     $23HOF THE PARAMETER CARD .)
 1006 FORMAT(8A1)
 1007 FORMAT(I4,6X,A40)
 2003 FORMAT(' PRINT ITEM DIAGRAMS .............',A3)
 2001 FORMAT(' PRINT EXTERNAL TRAIT MAPS .......',A3)
 2002 FORMAT(' LIMIT NO. OF ITERATIONS ( 0=',I2,' )...',I2)
 2010 FORMAT (' ** ERROR **', I6,1X,A,' is excessive.',
     $        ' Maximum allowed is',I5,'.')
C
      END

      SUBROUTINE CLOSEFL
C
      COMMON/IO/NTAPE,NREAD,NWRITE,NSCR
      COMMON/SHEMOR/INDIC, IFSHMR
C
      CLOSE (NREAD)
C
      CLOSE (NTAPE)
C
      CLOSE (NWRITE)
C
      CLOSE (NSCR)
C
      CLOSE (INDIC)
C
      RETURN
C
      END
